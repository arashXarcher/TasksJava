apply plugin: 'java'
version "0.2.1-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.apache.commons:commons-lang3:3.4'
}
apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}

tasks['uploadArchives'].onlyIf { project.hasProperty('signing.keyId')}
tasks['signArchives'].onlyIf { project.hasProperty('signing.keyId')}

group = rootProject.group
archivesBaseName = project.name.toLowerCase()

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'TasksJava'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A library for simplifying asynchronous programming using promises/futures'
                url 'https://github.com/s-arash/TasksJava'

                scm {
                    connection 'https://github.com/s-arash/TasksJava.git'
                    developerConnection 'https://github.com/s-arash/TasksJava'
                    url 'https://github.com/s-arash/TasksJava'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        //id 'arash.sahebolamri'
                        name 'Arash Sahebolamri'
                        email 'arash.sahebolamri@gmail.com'
                    }
                }
            }
        }
    }
}